plugins {
    id "com.github.johnrengelman.shadow" version "${shadow_version}"
}

def wynntils = {
    var url = "https://github.com/Wynntils/Artemis/releases/download/v1.2.31/wynntils-1.2.31-forge+MC-${minecraft_version}.jar"
    var name = "wynntils-1.2.31"

    File file = new File("$projectDir/libs/artemis/${name}.jar")
    file.parentFile.mkdirs()
    if (!file.exists()) {
        new URL(url).withInputStream { downloadStream ->
            file.withOutputStream { fileOut ->
                fileOut << downloadStream
            }
        }

        File modsFile = new File("$projectDir/run/mods/wynntils.jar")
        modsFile.parentFile.mkdirs()

        var os = new FileInputStream(file)

        modsFile.withOutputStream { fileOut ->
            fileOut << os
        }
    }

    files(file.absolutePath)
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
    forge {
        mixinConfig("sequoia.mixins.json", "sequoia.mixins.forge.json")

        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${forge_version}"
    modRuntimeOnly "me.djtheredstoner:DevAuth-forge-latest:${devauth_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixinextras_version}"))
    implementation(include("io.github.llamalad7:mixinextras-forge:${mixinextras_version}"))

    modCompileOnly wynntils()
}

processResources {
    inputs.property "version", rootProject.version

    filesMatching("META-INF/mods.toml") {
        expand "version": rootProject.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [
        project.configurations.shadowCommon
    ]

    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "forge+MC-${minecraft_version}"
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
