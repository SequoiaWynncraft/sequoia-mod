def wynntils = {
    var url = "https://github.com/Wynntils/Wynntils/releases/download/v${wynntils_version}/wynntils-${wynntils_version}-fabric+MC-${minecraft_version}.jar"

    File file = new File("$projectDir/libs/artemis/${wynntils_version}.jar")
    file.parentFile.mkdirs()
    if (!file.exists()) {
        new URL(url).withInputStream { downloadStream ->
            file.withOutputStream { fileOut ->
                fileOut << downloadStream
            }
        }

        File modsFile = new File("$projectDir/run/mods/wynntils.jar")
        modsFile.parentFile.mkdirs()

        var os = new FileInputStream(file)

        modsFile.withOutputStream { fileOut ->
            fileOut << os
        }
    }

    files(file.absolutePath)
}

dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

    // This dependency is provided by forge naturally and by fabric in its build.gradle
    compileOnly "net.neoforged:bus:${neoforge_eventbus_version}"

    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixinextras_version}"))

    modCompileOnly wynntils()

    modImplementation "io.wispforest:owo-lib:${project.owo_lib_version}"
    annotationProcessor "io.wispforest:owo-lib:${project.owo_lib_version}"
}

architectury {
    common(enabled_platforms.split(","))
}
